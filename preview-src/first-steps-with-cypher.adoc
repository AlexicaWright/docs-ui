= First Steps with Cypher
:neo4j-version: 3.5
:author: Michael Hunger
:style: #65b144/#58b535/white:Person(name),#008cc1/#0f5788/white:Database(name)
:page-layout: graphgist
:page-neo4j-version: 3.5
:page-theme: graphgists

== Create a Record for Yourself

[source,cypher,role="runnable instant single backend:graphgist"]
----
CREATE (you:Person {name:"You"})
RETURN you
----

`CREATE` creates nodes with labels and properties.

You like _Neo4j_, right?

Let's find ourselves and add a new relationship to a new node.

[source,cypher,role="runnable instant single backend:graphgist"]
----
MATCH  (you:Person {name:"You"})
CREATE (you)-[like:LIKE]->(neo:Database {name:"Neo4j" })
RETURN you,like,neo
----

`CREATE` can create single nodes, or more complex structures.


== Create Your Friends

[source,cypher,role="runnable instant single backend:graphgist"]
----
MATCH (you:Person {name:"You"})
FOREACH (name in ["Johan","Rajesh","Anna","Julia","Andrew"] |
  CREATE (you)-[:FRIEND]->(:Person {name:name}))
----

`FOREACH` allows you to execute update operations for each element of a list.

== Find Your Friends

[source,cypher,role="runnable graph backend:graphgist"]
----
MATCH (you {name:"You"})-[friendship:FRIEND]->(yourFriends)
RETURN you, yourFriends
----

// NOTE: nodes won't be attached/linked, unless we explicitly return the "friendship" relation: "RETURN you, friendship, yourFriends"
// in runnable.js, if we remove "res.links.filter((link) => link.selected)" (line 118) then nodes will be attached.

Note that we get ourselves repeated for each path found in the graph.

== Create Second Degree Friends and Expertise

[source,cypher,role="runnable instant single backend:graphgist"]
----
MATCH (neo:Database {name:"Neo4j"})
MATCH (anna:Person {name:"Anna"})
CREATE (anna)-[:FRIEND]->(:Person:Expert {name:"Amanda"})-[:WORKED_WITH]->(neo)
----

`CREATE` can also add more complex patterns.

== Find Someone in your Network Who Can Help You Learn Neo4j

[source,cypher,role="runnable graph backend:graphgist"]
----
MATCH (you {name:"You"})
MATCH (expert)-[:WORKED_WITH]->(db:Database {name:"Neo4j"})
MATCH path = shortestPath( (you)-[:FRIEND*..5]-(expert) )
RETURN db,expert,path
----
